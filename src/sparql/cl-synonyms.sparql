prefix owl: <http://www.w3.org/2002/07/owl#>
prefix oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?cls ?pred ?val ?synType
WHERE 
  { ?cls ?pred ?val ;
         a owl:Class .
    FILTER (
    ?pred = rdfs:label ||
    ?pred = oboInOwl:hasRelatedSynonym ||
    ?pred = oboInOwl:hasNarrowSynonym ||
    ?pred = oboInOwl:hasBroadSynonym ||
    ?pred = oboInOwl:hasExactSynonym
    )

    OPTIONAL {
      [
       a owl:Axiom ;
         owl:annotatedSource ?cls ;
         owl:annotatedProperty ?pred ;
         owl:annotatedTarget ?val ;
         oboInOwl:hasSynonymType ?synType
      ]
    }
    FILTER NOT EXISTS { ?cls owl:deprecated "true"^^xsd:boolean . }
    FILTER(isIRI(?cls) && STRSTARTS(str(?cls), "http://purl.obolibrary.org/obo/CL_") || STRSTARTS(str(?cls), "http://purl.obolibrary.org/obo/cl#"))
  }
